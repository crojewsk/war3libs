/*
*  SmoothItemPickup v1.0.0.8
*     by Bannar
*
*  Allows for item pickup despite unit inventory being full.
*/
package SmoothItemPickup
import LinkedList
import LinkedListModule
import HashMap
import RegisterEvents
import ClosureTimers
import Orders

let conditions = new LinkedList<SmoothPickupPredicate>()
let map = new HashMap<int, PeriodicData>()
timer looper = CreateTimer()
// Event related globals
unit eventUnit = null
item eventItem = null
trigger eventTrigger = CreateTrigger()

/** Default pickup range degined in gameplay constants. */
@configurable int PICK_UP_RANGE = 150

public interface SmoothPickupPredicate
    /** Determinates whether unit can pickup specified item. */
    function canPickup(unit whichUnit, item whichItem) returns boolean

class PeriodicData
    use LinkedListModule
    unit picker
    item itm
    real range

    construct(unit u, real range)
        this.picker = u
        this.range = range
        map.put(u.getHandleId(), this)

    ondestroy
        map.remove(picker.getHandleId())
        if size == 1
            looper.pause()

/** Returns unit attempting to pickup event item. */
public function getSmoothItemPickupUnit() returns unit
    return eventUnit

/** Returns item that is being picked up. */
public function getSmoothItemPickupItem() returns item
    return eventItem

public function registerSmoothItemPickupEvent(code func)
    eventTrigger.addCondition(Condition(func))

/** Adds new condition for item to be picked up smoothly. Conditions are aggregated in 'OR' fashion. */
public function addSmoothItemPickupCondition(SmoothPickupPredicate predicate)
    if predicate == null
        return
    conditions.add(predicate)

/** Removes specified condition from predicate list. */
public function removeSmoothItemPickupCondition(SmoothPickupPredicate predicate)
    if predicate == null
        return
    conditions.remove(predicate)

function fireEvent(unit u, item itm)
    let prevUnit = eventUnit
    let prevItem = eventItem
    eventUnit = u
    eventItem = itm

    eventTrigger.evaluate()

    eventUnit = prevUnit
    eventItem = prevItem

function test(unit u, item itm, real range) returns boolean
    if u.hasItem(itm)
        return true

    let dx = itm.getX() - u.getX()
    let dy = itm.getY() - u.getY()
    // Assumes range is multipled to avoid invoking SquareRoot
    return (dx * dx +  dy * dy) <= range

function onCallback()
    var iter = PeriodicData.iterator()
    while iter.hasNext()
        var data = iter.next()
        if not data.picker.isAlive() or data.picker.getCurrentOrder() != Orders.move or not data.itm.isPickupable()
            destroy data
        else
            if test(data.picker, data.itm, data.range)
                fireEvent(data.picker, data.itm)
                destroy data
    iter.close()

function onTargetOrder()
    unit u = GetTriggerUnit()
    item itm = GetOrderTargetItem()
    PeriodicData data
    bool proceed = false

    if not u.isInventoryFull() or GetIssuedOrderId() != SpecialOrders.smart
        return
    if itm == null or itm.isPowerup()
        return

    for condition in conditions
        if condition.canPickup(u, itm)
            proceed = true
            break    
    if not proceed
        return

    real collision = u.getCollisionSize()
    real range = (PICK_UP_RANGE + collision) * (PICK_UP_RANGE + collision)

    if test(u, itm, range)
        // Ensures order is finished before item is picked up.
        // Fixes the issue with unit moving towards the item location, rather than stopping
        nullTimer() ->
            if u.isAlive() and itm.isPickupable()
                fireEvent(u, itm)
    else // if unit is not nearby target item, issue artificial move order
        if not map.has(u.getHandleId())
            if PeriodicData.size == 0
                looper.startPeriodic(0.031250000, () -> onCallback())
            data = new PeriodicData(u, range)
        else
            data = map.get(u.getHandleId())
        data.itm = itm

        var angle = bj_RADTODEG * Atan2(u.getY() - itm.getY(), u.getX() - itm.getX())
        var x = itm.getX() + PICK_UP_RANGE * Cos(angle * bj_DEGTORAD)
        var y = itm.getY() + PICK_UP_RANGE * Sin(angle * bj_DEGTORAD)
        trigger t = getPlayerUnitEventTrigger(EVENT_PLAYER_UNIT_ISSUED_POINT_ORDER)
        t.disable()
        u.issuePointOrderById(Orders.move, vec2(x, y))
        t.enable()

init
    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_ISSUED_TARGET_ORDER, () -> onTargetOrder())
